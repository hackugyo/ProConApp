apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def proconConfig = project.rootProject.ext.config

android {

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "jp.ne.hatena.hackugyo.procon.app"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "boolean", "SSL_TRUST_ALL", "false"

        buildConfigField("String", "BOOTSTRAP_DATA_TIMESTAMP", "\"${bootstrap_data_timestamp}\"")
        buildConfigField("boolean", "IS_MOCK", "false")
        buildConfigField("boolean", "IS_STAGING", "false")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // siginingConfigs
    // app/signingConfigs/release.gradle を設定してください
    apply from: 'signingConfigs/release.gradle', to: android

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            debuggable true
            zipAlignEnabled true
            minifyEnabled false
            versionNameSuffix "-debug.${gitSha}"
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "ENABLE_STETHO", "true"
            ext.enableCrashlytics = false
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_STETHO", "false"
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions  "global_environment"

    productFlavors {
        mock {
            applicationId "jp.ne.hatena.hackugyo.procon.app.mock"
            resValue "string", "app_name", "${proconConfig.appName.mock}"
            buildConfigField "boolean", "SSL_TRUST_ALL", "true"
            buildConfigField "boolean", "IS_MOCK", "true"

            dimension "global_environment"
        }
        staging {
            applicationId 'jp.ne.hatena.hackugyo.procon.app.staging'
            resValue "string", "app_name", "${proconConfig.appName.staging}"
            buildConfigField "boolean", "ENABLE_LVL", "false"
            buildConfigField "boolean", "IS_STAGING", "true"

            dimension "global_environment"
        }
        product {
            buildConfigField "boolean", "ENABLE_LVL", "true"
            resValue "string", "app_name", "${proconConfig.appName.production}"

            dimension "global_environment"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets { debug { java.srcDirs = ['src/debug/java', 'src/debug/java/'] } }
}

final SUPPORT_LIBRARY_VERSION = '23.1.1'
final OKHTTP_VERSION = '2.7.4'
// final OKHTTP3_VERSION = '3.1.2'
final RETROFIT_VERSION = '2.0.0-beta2'
final PARCELER_VERSION = '1.0.4'

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    }
}

repositories {
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
    jcenter()
}

dependencies {
    // basic
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"

    // Joda-Time. All of Calendar, GregorianCalendar and Time are not good parts.
    compile 'net.danlew:android.joda:2.9.1'

    // JSON utility library.
    compile 'com.google.code.gson:gson:2.3'

    // Netwoking libraries.
    compile "com.squareup.okhttp:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp:logging-interceptor:$OKHTTP_VERSION"
    compile "com.squareup.okhttp:okhttp-urlconnection:$OKHTTP_VERSION"
    //  compile "com.squareup.okhttp3:okhttp-urlconnection:$OKHTTP3_VERSION"
    compile "com.squareup.retrofit:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.retrofit:converter-gson:$RETROFIT_VERSION"

    compile 'com.squareup.picasso:picasso:2.5.2'

    // rx
    compile 'io.reactivex:rxjava:1.0.15'
    compile 'io.reactivex:rxandroid:1.0.1'
    // rx view binding
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    // rx bus
    compile 'com.github.kubode:rxeventbus:1.0.1'

    // Database library.
    compile 'io.realm:realm-android:0.87.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'

    // Url Preview
    compile 'org.jsoup:jsoup:1.8.3' // required
    compile 'com.leocardz:link-preview:1.2.1@aar'

    // Parcelablecompile
    compile "org.parceler:parceler-api:$PARCELER_VERSION"
    apt "org.parceler:parceler:$PARCELER_VERSION"

    // UI Library.
    compile 'com.beardedhen:androidbootstrap:2.1.0'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.2.0'

    // for junit testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    // for android unit testing
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    // for ui testing
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.9'
}
